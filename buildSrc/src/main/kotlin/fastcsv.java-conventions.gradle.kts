plugins {
    java
    checkstyle
    pmd
    id("com.github.spotbugs")
}

repositories {
    mavenCentral()
}

tasks.withType<com.github.spotbugs.snom.SpotBugsTask>().configureEach {
    excludeFilter = file("${project.rootDir}/config/spotbugs/config.xml")
    reports.maybeCreate("xml").required = false
    reports.maybeCreate("html").required = true
}

tasks.withType<JavaCompile>().configureEach {
    // Add parameter names to the generated bytecode
    // It helps JUnit to include parameter names when building ParameterizedTest names
    options.compilerArgs.add("-parameters")
}

pmd {
    // Version bundled with Gradle is not able to run on Java 24.
    toolVersion = "7.13.0"
    isConsoleOutput = true
    ruleSets = emptyList()
    ruleSetFiles = files("${project.rootDir}/config/pmd/config.xml")
}
