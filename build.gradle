plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.5.0'
    id 'me.champeau.gradle.jmh' version '0.5.1'
}

group = "de.siegmar"
version = "2.0.0-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.apache.commons:commons-lang3:3.11'
    testImplementation 'commons-io:commons-io:2.8.0'
}

tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

jmh {
    warmupIterations = 2
    iterations = 5
    benchmarkMode = ['thrpt']
    fork = 2
    operationsPerInvocation = 1
}

if (hasProperty('ossrhUsername')) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'fastcsv'
                from components.java

                pom {
                    name = 'FastCSV'
                    description = 'Ultra fast and simple RFC 4180 compliant CSV library.'
                    url = 'https://github.com/osiegmar/FastCSV'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    scm {
                        url = 'https://github.com/osiegmar/FastCSV'
                        connection = 'scm:git:https://github.com/osiegmar/FastCSV.git'
                    }
                    developers {
                        developer {
                            id = 'osiegmar'
                            name = 'Oliver Siegmar'
                            email = 'oliver@siegmar.de'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
